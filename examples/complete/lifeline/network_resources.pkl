/*
 * Â© 2025 Platform Engineering Labs Inc.
 *
 * SPDX-License-Identifier: FSL-1.1-ALv2
 */

import "@formae/formae.pkl"
import "@aws/ec2/vpc.pkl"
import "@aws/ec2/internetgateway.pkl"
import "@aws/ec2/vpcgatewayattachment.pkl"
import "@aws/ec2/subnet.pkl"
import "@aws/ec2/routetable.pkl"
import "@aws/ec2/route.pkl"
import "@aws/ec2/subnetroutetableassociation.pkl"

class NetworkResources {
    name: String
    vpcLabel: String
    vpcCidr: String
    subnetCidr1: String
    subnetCidr2: String
    region: String

    hidden vpc: vpc.VPC = new {
        label = vpcLabel
        cidrBlock = vpcCidr
        enableDnsHostnames = true
        enableDnsSupport = true
        tags { new { key = "Name"; value = formae.value(name + "-vpc").setOnce } }
    }

    hidden igw: internetgateway.InternetGateway = new {
        label = "lifeline-igw"
        tags { new { key = "Name"; value = formae.value(name + "-igw").setOnce } }
    }

    hidden attachIgw: vpcgatewayattachment.VPCGatewayAttachment = new {
        label = "lifeline-igw-attachment"
        vpcId = vpc.res.id
        internetGatewayId = igw.res.id
    }

    hidden subnet1: subnet.Subnet = new {
        label = "lifeline-public-subnet-1"
        vpcId = vpc.res.id
        cidrBlock = subnetCidr1
        availabilityZone = "\(region)a"
        mapPublicIpOnLaunch = true
        tags { new { key = "Name"; value = formae.value(name + "-public-subnet-1").setOnce } }
    }

    hidden subnet2: subnet.Subnet = new {
        label = "lifeline-public-subnet-2"
        vpcId = vpc.res.id
        cidrBlock = subnetCidr2
        availabilityZone = "\(region)b"
        mapPublicIpOnLaunch = true
        tags { new { key = "Name"; value = formae.value(name + "-public-subnet-2").setOnce } }
    }

    hidden routeTable: routetable.RouteTable = new {
        label = "lifeline-public-rt"
        vpcId = vpc.res.id
        tags { new { key = "Name"; value = formae.value(name + "-public-rt").setOnce } }
    }

    hidden publicRoute: route.Route = new {
        label = "lifeline-public-route"
        routeTableId = routeTable.res.id
        destinationCidrBlock = "0.0.0.0/0"
        gatewayId = igw.res.id
    }

    hidden subnetAssoc1: subnetroutetableassociation.SubnetRouteTableAssociation = new {
        label = "lifeline-public-subnet-1-assoc"
        subnetId = subnet1.res.subnetId
        routeTableId = routeTable.res.id
    }

    hidden subnetAssoc2: subnetroutetableassociation.SubnetRouteTableAssociation = new {
        label = "lifeline-public-subnet-2-assoc"
        subnetId = subnet2.res.subnetId
        routeTableId = routeTable.res.id
    }

    hidden resources: Listing<formae.Resource> = new {
        vpc
        igw
        attachIgw
        subnet1
        subnet2
        routeTable
        publicRoute
        subnetAssoc1
        subnetAssoc2
    }
}

