/*
 * Â© 2025 Platform Engineering Labs Inc.
 *
 * SPDX-License-Identifier: FSL-1.1-ALv2
 */

import "@aws/aws.pkl"
import "@aws/ec2/vpc.pkl"
import "@aws/ec2/subnet.pkl"
import "@aws/ec2/securitygroup.pkl"
import "@aws/efs/filesystem.pkl"
import "@aws/efs/mounttarget.pkl"
import "@aws/efs/accesspoint.pkl"
import "@aws/iam/role.pkl"
import "@aws/sagemaker/domain.pkl"
import "@aws/sagemaker/userprofile.pkl"
import "@aws/sagemaker/modelpackagegroup.pkl"

class SageMakerPlatform {
  name: String
  region: aws.Region
  accountId: String
  vpc: vpc.VPC
  privateSubnets: Listing<subnet.Subnet>
  mlSecurityGroup: securitygroup.SecurityGroup
  efsSecurityGroup: securitygroup.SecurityGroup
  fileSystem: filesystem.FileSystem
  mountTargets: Listing<mounttarget.MountTarget>
  accessPoints: Listing<accesspoint.AccessPoint>
  sageMakerExecutionRole: role.Role
  dataScienceRole: role.Role

  hidden domain: domain.Domain = new {    
    label = "\(name)-domain"
    domainName = "\(name)-domain"
    authMode = "IAM"
    vpcId = vpc.res.id
    subnetIds {
      for (subnet in privateSubnets) {
        subnet.res.subnetId
      }
    }
    appNetworkAccessType = "VpcOnly"
    defaultUserSettings {
      executionRole = sageMakerExecutionRole.res.arn
      securityGroups {
        mlSecurityGroup.res.groupId
        efsSecurityGroup.res.groupId
      }
      autoMountHomeEFS = "Disabled"
      customFileSystemConfigs {
        new {
          efsFileSystemConfig {
            // Force dependency on mountTarget creation
            fileSystemId = mountTargets.first.res.fileSystemId
            fileSystemPath = "/200005"
          }
        }
      }
    }
    tags {
      new { key = "Name"; value = label }
      new { key = "ManagedBy"; value = "formae" }
    }
  }

  hidden dataScientistProfile: userprofile.UserProfile = new {
    label = "\(name)-data-scientist-profile"
    domainId = domain.res.domainId
    userProfileName = "data-scientist"
    userSettings {
      executionRole = dataScienceRole.res.arn
      securityGroups {
        mlSecurityGroup.res.groupId
        efsSecurityGroup.res.groupId
      }
      autoMountHomeEFS = "Disabled"
      customFileSystemConfigs {
        new {
          efsFileSystemConfig {
            fileSystemId = fileSystem.res.fileSystemId
            fileSystemPath = "/200010"
          }
        }
      }
    }
    tags {
      new { key = "Name"; value = "data-scientist" }
      new { key = "ManagedBy"; value = "formae" }
    }
  }

  hidden mlOpsProfile: userprofile.UserProfile = new {
    label = "\(name)-ml-ops-profile"
    domainId = domain.res.domainId
    userProfileName = "ml-ops"
    userSettings {
      executionRole = sageMakerExecutionRole.res.arn
      securityGroups {
        mlSecurityGroup.res.groupId
        efsSecurityGroup.res.groupId
      }
      autoMountHomeEFS = "Disabled"
      customFileSystemConfigs {
        new {
          efsFileSystemConfig {
            fileSystemId = fileSystem.res.fileSystemId
            fileSystemPath = "/200005"
          }
        }
      }
    }
    tags {
      new { key = "Name"; value = "ml-ops" }
      new { key = "ManagedBy"; value = "formae" }
    }
  }

  hidden modelPackageGroup: modelpackagegroup.ModelPackageGroup = new {
    label = "\(name)-production-models"
    modelPackageGroupName = "\(name)-production-models"
    modelPackageGroupDescription = "Production-ready models for the ML platform"
    tags {
      new { key = "Name"; value = label }
      new { key = "ManagedBy"; value = "formae" }
    }
  }

  resources: Listing = new {
    domain
    dataScientistProfile
    mlOpsProfile
    modelPackageGroup
  }
}