/*
 * Â© 2025 Platform Engineering Labs Inc.
 *
 * SPDX-License-Identifier: FSL-1.1-ALv2
 */

import "@formae/formae.pkl"
import "@aws/aws.pkl"

import "networking.pkl"
import "storage.pkl"
import "security.pkl"
import "monitoring.pkl"
import "containers.pkl"
import "sagemaker.pkl"

// This class encapsulates the entire ML Platform infrastructure
// It composes various components - networking, storage, security, monitoring, 
// containers, and SageMaker - into a single deployable unit.
class MLPlatform {
  name: String
  region: aws.Region
  accountId: String
  vpcCidr: String = "10.0.0.0/16"
  
  publicZoneCidrs: Mapping<String, String>
  privateZoneCidrs: Mapping<String, String>
  
  dbInstanceClass: String = "db.t3.medium"
  dbUsername: String = "dbadmin"

  hidden networking: networking.Networking = new {
    name = outer.name
    region = outer.region
    vpcCidr = outer.vpcCidr
    publicZoneCidrs = outer.publicZoneCidrs
    privateZoneCidrs = outer.privateZoneCidrs
  }

  hidden storage: storage.Storage = new {
    name = outer.name
    region = outer.region
    accountId = outer.accountId
    vpcId = networking.vpc.res.id
    privateSubnets = networking.privateSubnets
    efsSecurityGroup = networking.efsSecurityGroup
    dbSecurityGroup = networking.dbSecurityGroup
    dbInstanceClass = outer.dbInstanceClass
    dbUsername = outer.dbUsername
  }

  hidden security: security.Security = new {
    name = outer.name
    kmsKey = storage.kmsKey
  }

  hidden monitoring: monitoring.Monitoring = new {
    name = outer.name
    kmsKey = storage.kmsKey
  }

  hidden containers: containers.Containers = new {
    name = outer.name
    kmsKey = storage.kmsKey
  }

  hidden sageMaker: sagemaker.SageMakerPlatform = new {
    name = outer.name
    region = outer.region
    accountId = outer.accountId
    vpc = networking.vpc
    privateSubnets = networking.privateSubnets
    mlSecurityGroup = networking.mlSecurityGroup
    efsSecurityGroup = networking.efsSecurityGroup
    fileSystem = storage.fileSystem
    mountTargets = storage.mountTargets
    accessPoints = storage.accessPoints
    sageMakerExecutionRole = security.sageMakerExecutionRole
    dataScienceRole = security.dataScienceRole
  }

  resources: Listing = new {
    ...networking.resources
    ...storage.resources
    ...security.resources
    ...monitoring.resources
    ...containers.resources
    ...sageMaker.resources
  }
}