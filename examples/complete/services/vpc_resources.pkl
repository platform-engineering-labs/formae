/*
 * Â© 2025 Platform Engineering Labs Inc.
 *
 * SPDX-License-Identifier: FSL-1.1-ALv2
 */

import "@formae/formae.pkl"
import "@aws/ec2/vpc.pkl" as awsvpc
import "@aws/ec2/vpcgatewayattachment.pkl"
import "@aws/ec2/subnet.pkl"
import "@aws/ec2/internetgateway.pkl"
import "@aws/ec2/routetable.pkl"
import "@aws/ec2/route.pkl"
import "@aws/ec2/subnetroutetableassociation.pkl"

import "./types.pkl"

class VpcResources {
    team: types.Team
    
    hidden vpc: awsvpc.VPC = new {
        label = "\(team)-vpc"
        cidrBlock = "10.1.0.0/16"
        enableDnsHostnames = true
        enableDnsSupport = true
        tags { new { key = "Name"; value = "\(team)-vpc" } }
    }

    hidden igw: internetgateway.InternetGateway = new {
        label = "\(team)-igw"
        tags { new { key = "Name"; value = "\(team)-igw" } }
    }

    hidden attachIgw: vpcgatewayattachment.VPCGatewayAttachment = new {
        label = "\(team)-igw-attachment"
        vpcId = vpc.res.id
        internetGatewayId = igw.res.id
    }

    hidden subnet1: subnet.Subnet = new {
        label = "\(team)-public-subnet-1"
        vpcId = vpc.res.id
        cidrBlock = "10.1.1.0/24"
        availabilityZone = (types.regionFromTeam(team) + "a")
        mapPublicIpOnLaunch = true
        tags { new { key = "Name"; value = "\(team)-public-subnet-1" } }
    }

    hidden subnet2: subnet.Subnet = new {
        label = "\(team)-public-subnet-2"
        vpcId = vpc.res.id
        cidrBlock = "10.1.2.0/24"
        availabilityZone = (types.regionFromTeam(team) + "b")
        mapPublicIpOnLaunch = true
        tags { new { key = "Name"; value = "\(team)-public-subnet-2" } }
    }

    hidden routeTable: routetable.RouteTable = new {
        label = "\(team)-public-rt"
        vpcId = vpc.res.id
        tags { new { key = "Name"; value = "\(team)-public-rt" } }
    }

    hidden publicRoute: route.Route = new {
        label = "\(team)-public-route"
        routeTableId = routeTable.res.id
        destinationCidrBlock = "0.0.0.0/0"
        gatewayId = igw.res.id
    }

    hidden subnetAssoc1: subnetroutetableassociation.SubnetRouteTableAssociation = new {
        label = "\(team)-public-subnet-1-assoc"
        subnetId = subnet1.res.subnetId
        routeTableId = routeTable.res.id
    }

    hidden subnetAssoc2: subnetroutetableassociation.SubnetRouteTableAssociation = new {
        label = "\(team)-public-subnet-2-assoc"
        subnetId = subnet2.res.subnetId
        routeTableId = routeTable.res.id
    }

    hidden resources: Listing<formae.Resource> = new {
        vpc
        igw
        attachIgw
        subnet1
        subnet2
        routeTable
        publicRoute
        subnetAssoc1
        subnetAssoc2
    }
}
