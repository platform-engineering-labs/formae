/*
 * Â© 2025 Platform Engineering Labs Inc.
 *
 * SPDX-License-Identifier: FSL-1.1-ALv2
 */

import "@formae/formae.pkl"
import "@aws/ec2/vpc.pkl"
import "@aws/rds/dbsubnetgroup.pkl"
import "@aws/ec2/keypair.pkl"
import "@aws/rds/dbinstance.pkl"
import "@aws/ec2/securitygroup.pkl"
import "@aws/ec2/securitygroupingress.pkl"
import "@aws/ec2/subnet.pkl"

import "./types.pkl"

class DatabaseResources {
    team: types.Team
    size: types.DbSize
    vpc: vpc.VPC
    subnet1: subnet.Subnet
    subnet2: subnet.Subnet

    hidden rdsSg: securitygroup.SecurityGroup = new {
        label = "\(team)-db-sg"
        groupDescription = "Allow MySQL access from EC2"
        vpcId = vpc.res.id
        tags {
            new { key = "Name"; value = "\(team)-db-sg" }
        }
    }

    hidden rdsSgIngress: securitygroupingress.SecurityGroupIngress = new {
        label = "\(team)-db-sg-ingress"
        ipProtocol = "tcp"
        fromPort = 3306
        toPort = 3306
        sourceSecurityGroupId = ec2Sg.res.groupId
        groupId = rdsSg.res.groupId
    }

    hidden ec2Sg: securitygroup.SecurityGroup = new {
        label = "\(team)-ec2-instance-sg"
        groupDescription = "Allow SSH and DB egress"
        vpcId = vpc.res.id
        tags {
            new { key = "Name"; value = "\(team)-ec2-instance-sg" }
        }
    }

    hidden ec2SgIngress: securitygroupingress.SecurityGroupIngress = new {
        label = "\(team)-ec2-rds-sg-ingress"
        ipProtocol = "tcp"
        fromPort = 22
        toPort = 22
        cidrIp = "0.0.0.0/0"
        groupId = ec2Sg.res.groupId
    }

    hidden dbSubnetGroup: dbsubnetgroup.DBSubnetGroup = new {
        label = "\(team)-db-subnet-group"
        dbSubnetGroupDescription = "Subnet group for RDS"
        subnetIds {subnet1.res.subnetId; subnet2.res.subnetId}
        tags {
            new { key = "Name"; value = "\(team)-db-subnet-group" }
        }
    }

    hidden kp: keypair.KeyPair = new {
        label = "\(team)-ec2-keypair"
        keyName = "\(team)-ec2-keypair"
    }

    hidden dbInstance: dbinstance.DBInstance = new {
        label = "\(team)-db-instance"
        dbInstanceIdentifier = "\(team)-db-instance"
        allocatedStorage = 20
        dbInstanceClass = types.dbRawSizeFromSize(size)
        engine = "mysql"
        engineVersion = "8.0"
        masterUsername = "admin"
        masterUserPassword = "ChangeMe123!"
        vpcSecurityGroups {rdsSg.res.groupId}
        dbSubnetGroupName = dbSubnetGroup.res.dbSubnetGroupName
        publiclyAccessible = true
        multiAZ = false
        tags {
            new { key = "Name"; value = "\(team)-db-instance" }
        }
    }

    hidden resources: Listing<formae.Resource> = new {
        rdsSg
        rdsSgIngress
        ec2Sg
        ec2SgIngress
        dbSubnetGroup
        kp
        dbInstance
    }
}
