/*
 * Â© 2025 Platform Engineering Labs Inc.
 *
 * SPDX-License-Identifier: FSL-1.1-ALv2
 */

module generator

import "pkl:reflect"
awsImports = import*("@aws/*/*.pkl")

open class Resource {
    type: String
    moduleName: String
    clazz: Class
    className: String = clazz.simpleName
    importName: String = moduleName.replaceAll(".pkl", "").split("/").last
}

function GetAllResourceTypes() = new Listing<Resource> {

    for (modName, moduleValue in awsImports.toMap()) {
        when(reflect.Module(moduleValue).classes.length > 0) {
            for (className, v in reflect.Module(moduleValue).classes) {
                when(v.annotations.length > 0 && v.annotations[0].getClass().simpleName == "ResourceHint") {
                    new Resource {
                        type = v.annotations[0].getProperty("type")
                        moduleName = "\(modName)"
                        clazz = v.reflectee
                    }
                }
            }
        }
    }
}

output {
    text = GetAllResourceTypes().toList().join("\n")
}