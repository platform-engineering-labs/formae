/*
 * Â© 2025 Platform Engineering Labs Inc.
 *
 * SPDX-License-Identifier: FSL-1.1-ALv2
 */

module generator

amends "pkl:test"
import "../gen.pkl" as gen
import "@formae/formae.pkl"
import "pkl:reflect" as reflect
import "@fakeaws/route53/hostedzone.pkl" as hostedzone
import "@fakeaws/apprunner/service.pkl" as apprunner
import "@fakeaws/shared/deploymentconfig.pkl" as deploymentconfig

/// Local Tag class for testing (mirrors the pattern used by resource plugins)
local class Tag {
    key: String
    value: String
}

@formae.ResourceHint
local class SimpleFoo extends formae.Resource{
    x: Int
    name: String
}

@formae.ResourceHint
local class ComplexBar extends formae.Resource {
    items: Listing<String>
    metadata: Mapping<String, Any>
    optional: String?
}

@formae.ResourceHint
local class TestResource extends formae.Resource {
    type = "FakePlugin::Service::TestResource"

    @formae.FieldHint
    size: Int?

    @formae.FieldHint
    tags: Listing<Tag>

    @formae.FieldHint
    bucketName: String
}

// Test data
local simpleProperties: Dynamic = new Dynamic {
   x = 42
   name = "test"
   label = "simple"
}

local complexProperties: Dynamic = new Dynamic {
    items = new Listing { "item1"; "item2"; "item3" }
    //metadata = new Mapping {
    //    ["key1"] = "value1"
    //    ["key2"] = 123
    //}
    optional = "optional value"
    label = "complex"
}

local resourceProperties: Dynamic = new Dynamic {
    bucketName = "my-test-bucket"
    size = 1024
    tags = new Listing {
        new Dynamic {
            key = "Environment"
            value = "test"
        }
        new Dynamic {
            key = "Owner"
            value = "team-a"
        }
    }
    label = "my-bucket"
}

examples {
    ["Apply simple class"] {
        gen.apply(SimpleFoo, simpleProperties)
    }

    ["Apply complex class with collections"] {
        gen.apply(ComplexBar, complexProperties)
    }

    //["ApplyResource for Simple class fails when it is no resource"] {
    //    gen.applyResource(SimpleFoo, simpleProperties)
    //}
    //
    //["ApplyResource for Complex class fails when it is no resource"] {
    //    gen.applyResource(ComplexBar, complexProperties)
    //}
    //
    //["Apply resource class"] {
    //    gen.applyResource(TestResource, resourceProperties)
    //}

    ["Convert to map with types - simple"] {
        gen.genPklToMapWithTypes(new SimpleFoo { x = 42; name = "test"; label = "simple" }.toDynamic())
    }

    ["Convert to map with types - complex"] {
        gen.genPklToMap(new TestResource {
            bucketName = "my-test-bucket"
            label = "my-bucket"
        }.toDynamic())
    }

    ["Generate import statements"] {
        gen.generateImportStatements(new Mapping {
                ["__import_statement__"] = "@aws/s3.pkl"
                ["__type__"] = "Bucket"
        }.toMap())
    }

    ["Collect imports when there are no imports"] {
        gen.generateImportStatements(new Mapping {
                ["__import_statement__"] = "@aws/s3.pkl"
                ["__type__"] = "Bucket"
        }.toMap())
    }
}

facts {
    ["Simple class application preserves values"] {
        let (result = gen.apply(SimpleFoo, simpleProperties))
        result.x == 42 && result.name == "test"
    }

    ["Complex class handles collections"] {
        let (result = gen.apply(ComplexBar, complexProperties))
        result.items.length == 3 &&
        result.optional == "optional value"
    }

    ["Resource class inherits from formae.Resource"] {
        let (result = gen.apply(TestResource, resourceProperties))
        result.bucketName == "my-test-bucket"
    }

    ["Map conversion includes type information"] {
        let (testObj = new TestResource { bucketName = "my-test-bucket"; size = 1024; label = "my-bucket" }.toDynamic())
        let (result = gen.genPklToMapWithTypes(testObj))
        //result["__type__"] == "FakeAWS::Suckets::TestResource" &&
        result["bucketName"] == "my-test-bucket" &&
        result["label"] == "my-bucket"
    }

    ["Import statement generation filters base types"] {
        let (parsedData = new Mapping {
            ["resource"] = new Mapping {
                ["__import__"] = "pkl:base"
                ["__type__"] = "String"
            }
        })
        let (imports = gen.generateImportStatements(parsedData.toMap()))
        !imports.contains("pkl:base")
    }

    ["Type conversion handles nullable types"] {
        let (result = gen.attemptApply(ComplexBar, new Dynamic {
            items = new Listing { "test" }
            optional = null
        }))
        !(result is gen.GenerationFailure) && (result as ComplexBar).optional == null
    }

    ["Should skip properties starting with double underscore"] {
        gen.shouldSkipPropertyName("__type__") == true &&
        gen.shouldSkipPropertyName("__import__") == true &&
        gen.shouldSkipPropertyName("__fullType__") == true &&
        gen.shouldSkipPropertyName("__metadata__") == true
    }

    ["Should skip system properties"] {
        gen.shouldSkipPropertyName("Properties") == true &&
        gen.shouldSkipPropertyName("Schema") == true &&
        gen.shouldSkipPropertyName("Fields") == true &&
        gen.shouldSkipPropertyName("output") == true
    }

    ["Should not skip regular properties"] {
        gen.shouldSkipPropertyName("bucketName") == false &&
        gen.shouldSkipPropertyName("label") == false &&
        gen.shouldSkipPropertyName("stack") == false &&
        gen.shouldSkipPropertyName("target") == false &&
        gen.shouldSkipPropertyName("size") == false &&
        gen.shouldSkipPropertyName("tags") == false &&
        gen.shouldSkipPropertyName("myCustomProperty") == false
    }

    ["Should handle edge cases"] {
        gen.shouldSkipPropertyName("") == false &&
        gen.shouldSkipPropertyName("_singleUnderscore") == false &&
        gen.shouldSkipPropertyName("property__") == false &&
        gen.shouldSkipPropertyName("PropertiesExtended") == false
    }

    ["parseModuleUri extracts correct import name from package URIs"] {
        gen.parseModuleUri("@oci/oci.pkl") == "oci" &&
        gen.parseModuleUri("@oci/objectstorage/bucket.pkl") == "bucket" &&
        gen.parseModuleUri("@aws/aws.pkl") == "aws" &&
        gen.parseModuleUri("@aws/types.pkl") == "types" &&
        gen.parseModuleUri("@azure/resources/resourcegroup.pkl") == "resourcegroup"
    }

    ["parseModuleUri handles full package URIs with fragments"] {
        gen.parseModuleUri("package://hub.example.com/pkg@1.0.0#/oci.pkl") == "oci" &&
        gen.parseModuleUri("package://hub.example.com/pkg@1.0.0#/objectstorage/bucket.pkl") == "bucket"
    }

    ["getImportInfo uses reflection to get module URI"] {
        // For a class defined in this test module (local file), getImportInfo returns
        // the placeholder since local file:// URIs aren't package URIs.
        // The important thing is that reflection works - parseModuleUri tests verify the parsing.
        let (tagClass = Tag.getClass())
        let (importInfo = gen.getImportInfo(tagClass))
        // Local file URIs return placeholder (which is expected)
        // For package URIs like @oci/oci.pkl, it would return "oci"
        importInfo == gen.importPlaceholder
    }

    // --- SubResource import statement tests ---

    ["generateImportStatements collects SubResource imports"] {
        // HostedZoneConfig is a SubResource defined in @fakeaws/route53/hostedzone.pkl.
        // parseValueEnhanced should add __import_statement__ for it so that
        // generateImportStatements picks it up.
        let (hz = new hostedzone.HostedZone {
            label = "test-zone"
            name = "example.com"
            hostedZoneConfig = new hostedzone.HostedZoneConfig {
                comment = "test comment"
            }
        })
        let (pklMap = gen.genPklToMap(hz.toDynamic()))
        let (imports = gen.generateImportStatements(pklMap))
        imports.contains("@fakeaws/route53/hostedzone.pkl")
    }

    ["getImportInfo resolves package URI for SubResource class"] {
        // HostedZoneConfig is defined in @fakeaws/route53/hostedzone.pkl.
        // getImportInfo should return "hostedzone" (the simple module name).
        let (importInfo = gen.getImportInfo(hostedzone.HostedZoneConfig))
        importInfo == "hostedzone"
    }

    ["getPackageName resolves package name for SubResource class"] {
        // HostedZoneConfig lives in the fakeaws package.
        gen.getPackageName(hostedzone.HostedZoneConfig) == "fakeaws"
    }

    // --- Generalized import statement tests (plain Typed, not SubResource) ---

    ["generateImportStatements collects plain Typed imports"] {
        // DeploymentConfig is a plain class (not SubResource) from @fakeaws/shared/deploymentconfig.pkl.
        // The generalized import logic should emit __import_statement__ for it.
        let (svc = new apprunner.Service {
            label = "test-svc"
            serviceName = "my-api"
            imageUri = "ecr.io/my-api:latest"
            deploymentConfig = new deploymentconfig.DeploymentConfig {
                environment = "production"
                version = "1.0.0"
                replicas = 2
            }
        })
        let (pklMap = gen.genPklToMap(svc.toDynamic()))
        let (imports = gen.generateImportStatements(pklMap))
        imports.contains("@fakeaws/shared/deploymentconfig.pkl")
    }

    ["getImportInfo resolves package URI for plain Typed class"] {
        let (importInfo = gen.getImportInfo(deploymentconfig.DeploymentConfig))
        importInfo == "deploymentconfig"
    }

    ["getPackageName resolves package name for plain Typed class"] {
        gen.getPackageName(deploymentconfig.DeploymentConfig) == "fakeaws"
    }

    ["duplicate Typed values produce single import after dedup"] {
        // Two HostedZoneConfig instances in the same resource should not
        // produce duplicate __import_statement__ entries after dedup.
        let (hz = new hostedzone.HostedZone {
            label = "test-zone"
            name = "example.com"
            hostedZoneConfig = new hostedzone.HostedZoneConfig {
                comment = "primary"
            }
            queryLoggingConfig = new hostedzone.QueryLoggingConfig {
                cloudWatchLogsLogGroupArn = "arn:aws:logs:us-east-1:123456789:log-group:test"
            }
        })
        let (pklMap = gen.genPklToMap(hz.toDynamic()))
        let (imports = gen.generateImportStatements(pklMap))
        let (hzImports = imports.filter((i) -> i == "@fakeaws/route53/hostedzone.pkl"))
        // collectImports may return duplicates but the caller (pklGenerator) dedups
        // Here we verify the imports are at least present
        imports.contains("@fakeaws/route53/hostedzone.pkl")
    }

}