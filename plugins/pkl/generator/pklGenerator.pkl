/*
 * Â© 2025 Platform Engineering Labs Inc.
 *
 * SPDX-License-Identifier: FSL-1.1-ALv2
 */

module FormaGenerator

import "./gen.pkl" as gen
import "@formae/formae.pkl"
import "./resources.pkl"
import "pkl:json"

resourceTypes = resources.GetAllResourceTypes()
typeMap = resourceTypes.fold(Map(), (acc: Map<String, resources.Resource>, elem) ->
    acc.put(elem.type, elem)
)

function headerImports(): String =
    """
    amends "@formae/forma.pkl"
    import "@formae/formae.pkl"
    """

function getImports(type: String, dependencies: List<String>): String =
    """
    amends "@formae/forma.pkl"
    import "@formae/formae.pkl"


    \(dependencies.map((dep) -> "import \"\(dep)\"").join("\n"))
    """

function toCamelCase(input: String): String =
    if (input == "$unmanaged")
        "myStack"
    else
        let (parts = input.split("-"))
        if (parts.length == 1)
            input
        else
            parts.first + parts.drop(1).map((part) ->
                if (part.isEmpty) ""
                else part.substring(0, 1).toUpperCase() + part.substring(1, part.length)
            ).join("")

function appendProperties(parsed: json.Value): json.Value =
    new Dynamic {
        ...parsed.Properties
        label = parsed.Label
    }

/// Extracts a valid PKL identifier alias from a module path
/// e.g., "@cloudflare-dns/cloudflare-dns.pkl" -> "cloudflare_dns"
/// e.g., "@aws/ec2/ec2.pkl" -> "ec2"
function modulePathToAlias(modulePath: String): String =
    modulePath.replaceAll(".pkl", "").split("/").last.replaceAll("-", "_")

function importFormatting(imports: List<String>): String =
    let (importsString = imports.map((i) ->
        let (alias = modulePathToAlias(i))
        "import \"\(i)\" as \(alias)"
    ))
    importsString.join("\n")

function parseStacks(stacksData: List<Map<String, Any>>, policyLabelMap: Map<String, String>): String =
    let (stacksString = stacksData.map((stackData) ->
        let (label = stackData["label"])
        let (camelCaseLabel = toCamelCase(label))
        let (description = stackData.getOrNull("description") ?? "This was generated")
        let (policies = stackData.getOrNull("policies") as List?)

        let (labelLine = if (label == "$unmanaged")
            """
            // Please provide a stack to bring the resources in this Forma under management
            // label = ""
            """
        else
            "        label = \"\(label)\""
        )

        // Generate policies line if stack has policy references or inline policies
        let (policiesLine = if (policies != null && policies.length > 0)
            let (policyEntries = policies.map((policy) ->
                // Check if it's a $ref (policy reference) or inline policy
                // Policy comes as Dynamic from JSON, access properties directly
                let (ref = policy.getPropertyOrNull("$ref") as String?)
                if (ref != null && ref.startsWith("policy://"))
                    // It's a policy reference - extract label and look up camelCase name
                    let (policyLabel = ref.replaceFirst("policy://", ""))
                    let (policyVarName = policyLabelMap.getOrNull(policyLabel) ?? toCamelCase(policyLabel))
                    "\(policyVarName).res"
                else
                    // Inline policy - render directly (without label, as it's typically auto-generated)
                    let (policyType = policy.getPropertyOrNull("Type") as String?)
                    if (policyType == "ttl")
                        let (ttlSeconds = policy.getPropertyOrNull("TTLSeconds") as Number? ?? 0)
                        let (onDependents = policy.getPropertyOrNull("OnDependents") as String? ?? "abort")
                        """
                        new formae.TTLPolicy {
                                    ttl = \(ttlSeconds).s
                                    onDependents = "\(onDependents)"
                                }
                        """
                    else
                        null
            ).filter((p) -> p != null))
            if (policyEntries.length > 0)
                "\n        policies = new Listing { \(policyEntries.join("; ")) }"
            else
                ""
        else
            ""
        )

        """

      local \(camelCaseLabel) = new formae.Stack {
        \(labelLine)
        description = "\(description)"\(policiesLine)
      }
      \(camelCaseLabel)
      """
    ))
    stacksString.join("\n")

/// Parses standalone policies and generates PKL output
function parsePolicies(policiesData: List<Map<String, Any>>): String =
    let (policiesString = policiesData.map((policyData) ->
        let (policyType = policyData["Type"] as String)
        let (label = policyData["Label"] as String)
        let (camelCaseLabel = toCamelCase(label))

        if (policyType == "ttl")
            let (ttlSeconds = policyData["TTLSeconds"] as Number)
            let (onDependents = policyData.getOrNull("OnDependents") as String? ?? "abort")
            """

      local \(camelCaseLabel) = new formae.TTLPolicy {
        label = "\(label)"
        ttl = \(ttlSeconds).s
        onDependents = "\(onDependents)"
      }
      \(camelCaseLabel)
      """
        else
            // Unknown policy type - skip with comment
            """

      // Skipped unknown policy type: \(policyType) (label: \(label))
      """
    ))
    policiesString.join("\n")

function parseTargets(targetsData: List<Map<String, Any>>): String =
    let (targetsString = targetsData.map((targetData) ->
        let (label = targetData["label"])
        let (discoverable = targetData.getOrNull("discoverable"))
        let (camelCaseLabel = toCamelCase(label))
        let (config = targetData["config"])
        let (namespace = targetData.getOrNull("namespace") ?? "aws")

        // Generate config properties if config exists
        let (configString = if (config != null)
            let (configMap = config.toMap())
            let (configLines = configMap.fold(List(), (acc: List<String>, key: String, value: Any) ->
                let (valueString = if (value is String) "\"\(value)\"" else value.toString())
                acc.add("      \(key) = \(valueString)")
            ))
            if (configLines.isEmpty) ""
            else
        """

          config {
        \(configLines.join("\n"))
            }
        """
        else ""
        )

        """

      local \(camelCaseLabel) = new formae.Target {
        label = "\(label)"
        namespace = "\(namespace)"\(if (discoverable != null) "\n        discoverable = \(discoverable)" else "")\(configString)
      }
      \(camelCaseLabel)
      """
    ))
    targetsString.join("\n")

function parseResources(resources: List<String>, targetMap: Map<String, String>, stackMap: Map<String, String>): String =
    let (resourcesString = resources.map((r) ->
        // Replace target references in the resource string with camelCase versions
        let (withTargets = targetMap.fold(r, (acc: String, originalLabel: String, camelCaseLabel: String) ->
            acc.replaceAll("target = \"\(originalLabel)\"", "target = \(camelCaseLabel).res")
        ))
        // Replace stack references in the resource string with camelCase versions
        stackMap.fold(withTargets, (acc: String, originalLabel: String, camelCaseLabel: String) ->
            acc.replaceAll("stack = \"\(originalLabel)\"", "stack = \(camelCaseLabel).res").replaceAll("stack = \"\(originalLabel).Label\"", "stack = \(camelCaseLabel).res.Label")
        )
    ))
    resourcesString.join("\n\n")

function generateFormaFile(parsed: json.Value): String =
    let (parsedList = parsed.Resources.toList())


    let (resourceData = parsedList.map((resource) ->
        let (stack = resource.Stack)
        let (target = resource.Target)
        let (resourceType = resource.Type)
        let (resourceLabel = resource.Label)
        let (className = typeMap[resourceType].className)
        let (importName = typeMap[resourceType].importName)
        let (properties = appendProperties(resource))
        let (pklMap = gen.genPklToMap(gen.applyResource(typeMap[resourceType].clazz, properties).toDynamic()).put("target", target).put("stack", stack))
        let (dependencyImports = gen.generateImportStatements(pklMap))
        let (resourceImport = typeMap[resourceType].moduleName)
        // Extract package name from resourceImport (e.g., "@aws/ec2/ec2.pkl" -> "aws", "@cloudflare-dns/cloudflare-dns.pkl" -> "cloudflare-dns")
        // The package name is the dependency alias and determines the base module import path
        let (packageName = resourceImport.split("/").first.replaceFirst("@", ""))

        let (rawPklResourceString = gen.genMapToPklStringWithTypes(className, pklMap, importName))

        let (pklResourceString = rawPklResourceString)

        Map(
            "stack", stack,
            "target", target,
            "resourceImport", resourceImport,
            "resourceType", resourceType,
            "namespaceImport", "@" + packageName + "/" + packageName + ".pkl",
            "dependencyImports", dependencyImports,
            "pklResourceString", pklResourceString
        )
    ))

    // Extract target data from the parsed JSON
    let (targetsFromJson = if (parsed.Targets != null)
        parsed.Targets.toList().map((target) -> Map(
            "label", target.Label,
            "namespace", target.Namespace ?? "aws",
            "config", target.Config,
            "discoverable", target.getPropertyOrNull("Discoverable")
            )
        )
    else
        List()
    )

    // Extract stack data from the parsed JSON, falling back to resource stack labels if Stacks is not present
    let (stacksFromJson = if (parsed.Stacks != null)
        parsed.Stacks.toList().map((stack) -> Map(
            "label", stack.Label,
            "description", stack.Description,
            "policies", stack.getPropertyOrNull("Policies")?.toList() ?? List()
            )
        )
    else
        // Fallback: extract stack labels from resources (for backwards compatibility)
        resourceData.map((r) -> r["stack"]).distinct.map((stackLabel) ->
            Map("label", stackLabel, "policies", List())
        )
    )

    // Extract standalone policies from the parsed JSON
    let (policiesFromJson = if (parsed.getPropertyOrNull("Policies") != null)
        parsed.Policies.toList().map((policy) -> Map(
            "Type", policy.Type,
            "Label", policy.Label,
            "TTLSeconds", policy.getPropertyOrNull("TTLSeconds"),
            "OnDependents", policy.getPropertyOrNull("OnDependents")
            )
        )
    else
        List()
    )

    // Create mapping from original labels to camelCase labels for targets
    let (targetLabelMap = targetsFromJson.fold(Map(), (acc: Map<String, String>, targetData) ->
        let (originalLabel = targetData["label"])
        let (camelCaseLabel = toCamelCase(originalLabel))
        acc.put(originalLabel, camelCaseLabel)
    ))

    // Create mapping from original labels to camelCase labels for stacks
    let (stackLabelMap = stacksFromJson.fold(Map(), (acc: Map<String, String>, stackData) ->
        let (originalLabel = stackData["label"])
        let (camelCaseLabel = toCamelCase(originalLabel))
        acc.put(originalLabel, camelCaseLabel)
    ))

    // Create mapping from original labels to camelCase labels for policies
    let (policyLabelMap = policiesFromJson.fold(Map(), (acc: Map<String, String>, policyData) ->
        let (originalLabel = policyData["Label"] as String)
        let (camelCaseLabel = toCamelCase(originalLabel))
        acc.put(originalLabel, camelCaseLabel)
    ))

    let (resourceImports = resourceData.map((r) -> r["resourceImport"] as String).distinct)
    // Get unique namespaces from resource types and add base module imports for Tag support (e.g., @azure/azure.pkl)
    let (namespaceBaseImports = resourceData.map((r) -> r["namespaceImport"] as String).distinct)
    // Combine and deduplicate - for flat package structures, resourceImport and namespaceImport may be the same
    let (imports = (resourceImports + namespaceBaseImports).distinct)
    let (dependencyImports = resourceData.map((r) -> r["dependencyImports"]).flatten().distinct.filter((e) -> !imports.contains(e)))
    let (resources = resourceData.map((r) -> r["pklResourceString"]))

    let (dependencyImportsSection = if (dependencyImports.length > 0)
        """

        // Dependency imports
        \(importFormatting(dependencyImports))
        """
    else
        ""
    )

    // Generate policies section (only if there are standalone policies)
    let (policiesSection = if (policiesFromJson.length > 0)
        parsePolicies(policiesFromJson) + "\n"
    else
        ""
    )

    """
    \(headerImports())

    // Resource imports
    \(importFormatting(imports))\(dependencyImportsSection)

    forma {\(policiesSection)
        \(parseStacks(stacksFromJson, policyLabelMap))

        \(parseTargets(targetsFromJson))

        \(parseResources(resources, targetLabelMap, stackLabelMap))
    }
    """

output {
    text = generateFormaFile(parsed)
}
